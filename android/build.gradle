group 'ai.passio.nutrition_ai'
version '1.0-SNAPSHOT'

buildscript {
    ext.kotlin_version = '1.9.10'
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:8.7.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

android {
    if (project.android.hasProperty("namespace")) {
        namespace 'ai.passio.nutrition_ai'
    }

    compileSdkVersion 34

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = 17
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }

    defaultConfig {
        minSdkVersion 26
    }

    dependencies {
        testImplementation 'org.jetbrains.kotlin:kotlin-test'
        testImplementation 'org.mockito:mockito-core:5.0.0'

        def useLocalPassioAar = project.hasProperty('useLocalPassioAar') ? project.useLocalPassioAar.toBoolean() : false

        if (useLocalPassioAar) {
            implementation(name: 'passiolib-release', ext: 'aar')

            // TensorFlow
            api 'org.tensorflow:tensorflow-lite:2.8.0'
            implementation 'org.tensorflow:tensorflow-lite-metadata:0.4.0'

            // CameraX
            def camerax_version = "1.3.0-alpha12"
            implementation "androidx.camera:camera-core:$camerax_version"
            implementation "androidx.camera:camera-camera2:$camerax_version"
            implementation "androidx.camera:camera-lifecycle:$camerax_version"
            api 'androidx.camera:camera-view:1.3.0-alpha02'
            implementation 'androidx.camera:camera-extensions:1.3.0-alpha02'

            // Barcode and OCR
            implementation 'com.google.android.gms:play-services-mlkit-text-recognition:18.0.2'
            implementation 'com.google.android.gms:play-services-mlkit-barcode-scanning:18.1.0'
        } else {
            implementation 'ai.passio.passiosdk:nutrition-ai:3.2.5'
        }
    }

    testOptions {
        unitTests.all {
            useJUnitPlatform()

            testLogging {
                events "passed", "skipped", "failed", "standardOut", "standardError"
                outputs.upToDateWhen { false }
                showStandardStreams = true
            }
        }
    }
}
